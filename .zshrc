# [[ -f /usr/share/oh-my-zsh/oh-my-zsh.sh ]] \
#    && source /usr/share/oh-my-zsh/oh-my-zsh.sh

export LA_ZSHRC_LOADED=$(($LA_ZSHRC_LOADED+1))

source $HOME/.bashrc

# Prompt {{{1
autoload -U promptinit && promptinit
setopt prompt_subst

## git prompt
## ref: http://stackoverflow.com/questions/1128496/to-get-a-prompt-which-indicates-git-branch-in-zsh
git_prompt() {
    git rev-parse --git-dir > /dev/null 2>&1 || return

    branch=$(git symbolic-ref HEAD 2>/dev/null | cut -d'/' -f3-)
    tag=$(git describe --tag 2>/dev/null)
    head=$(git rev-parse --short HEAD)

    if [[ ! -z ${branch} ]]; then
        prompt=${branch}
    elif [[ ! -z ${tag} ]]; then
        prompt=${tag}
    else
        prompt=${head}
    fi

    is_clean=$(git status --short)
    if [[ ! -z ${is_clean} ]]; then
        is_clean="%F{yellow}x"
    fi
    echo "%F{blue}git:(%F{red}${prompt}%F{blue}) ${is_clean}%f";
}

## host prompt
host_prompt(){
    if [[ -z ${SSH_CLIENT} ]]; then
        echo '%F{magenta}%m%f'
    else
        echo '%F{magenta}%m (%F{red}remote%F{magenta})%f'
    fi
}

PROMPT=$'%F{green}/%f %B%F{cyan}%n%f %F{white}@%f $(host_prompt) %F{black}%T%f %F{white}->%(?..%F{red}[%?])%f %F{green}%~%f %F$(git_prompt)%f %b
%F{green}\\%f %B%(!.%F{red}%#.%F{blue}$) %F{white}%b'

# History {{{1
HISTSIZE=100000
SAVEHIST=10000
## ä¸è®°å½•é‡å¤çš„å†å²
setopt HIST_IGNORE_DUPS
setopt share_history
HISTFILE=$HOME/.zsh_history
## åœ¨å‘½ä»¤å‰æ·»åŠ ç©ºæ ¼ï¼Œä¸å°†æ­¤å‘½ä»¤æ·»åŠ åˆ°è®°å½•æ–‡ä»¶ä¸­
setopt hist_ignore_space

# è¡¥å…¨ {{{1
autoload -U compinit && compinit

setopt AUTO_LIST
setopt AUTO_MENU
unset MENU_COMPLETE         # bash style complete
setopt complete_in_word     # complete /v/c/a/p
setopt no_nomatch		    # enhanced bash wildcard completion
setopt magic_equal_subst
setopt noautoremoveslash
setopt null_glob

## ä½¿ç”¨ç¼“å­˜ï¼ŒæŸäº›å‘½ä»¤çš„è¡¥å…¨å¾ˆè€—æ—¶çš„
zstyle ':completion:*' use-cache on
_cache_dir=${XDG_CACHE_HOME:-$HOME/.cache}/zsh
zstyle ':completion:*' cache-path $_cache_dir
unset _cache_dir

## æ–¹å‘é”®é€‰ä¸­å€™é€‰é¡¹
zstyle ':completion:*' menu select

zstyle ':completion::complete:feh:*' file-patterns '*.{gif,png,jpeg,jpg,svg}:images:images *(-/):directories:directories'


# ç›®å½• {{{1
setopt auto_cd      # if not a command, try to cd to it.

## ç›®å½•æ ˆ
DIRSTACKFILE="$HOME/.cache/zsh/dirs"
if [[ -f $DIRSTACKFILE ]] && [[ $#dirstack -eq 0 ]]; then
    dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
fi

chpwd() {
    print -l $PWD ${(u)dirstack} >$DIRSTACKFILE
}

DIRSTACKSIZE=20

setopt autopushd pushdsilent pushdtohome
## Remove duplicate entries
setopt pushdignoredups
## This reverts the +/- operators.
setopt pushdminus


# Key bilding {{{1
bindkey -e

## Functional key
bindkey  "${terminfo[khome]}"   beginning-of-line
bindkey  "${terminfo[kend]}"    end-of-line
bindkey  "${terminfo[kich1]}"   overwrite-mode
bindkey  "${terminfo[kbs]}"     backward-delete-char
bindkey  "${terminfo[kcuu1]}"   up-line-or-history
bindkey  "${terminfo[kcud1]}"   down-line-or-history
bindkey  "${terminfo[kcub1]}"   backward-char
bindkey  "${terminfo[kcuf1]}"   forward-char
bindkey  "${terminfo[kdch1]}"   delete-char # original: kdch1=\E[3~


## <- and -> (Linux only?)
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

## Search in history (thx lilydjwg :D)
bindkey '^[p' up-line-or-search
bindkey '^[n' down-line-or-search


# Plugins {{{1

## zsh-syntax-highlighting
[[ -f /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]] \
    && source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

## fzf
source /usr/share/fzf/key-bindings.zsh
source /usr/share/fzf/completion.zsh

## zsh-autosuggestions
source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
bindkey '^j' autosuggest-accept

## sphinxnotes-snippet
eval "$(snippet integration --zsh-binding)"

### Override snippet_z_url
function la_snippet_url() {
    selection=$(snippet_list ds)
    [ -z "$selection" ] && return

    echo "$BROWSER \$($SNIPPET get --url $selection)"
    sleep 0.2 && swaymsg workspace "1.web: ğŸŒ" 1>/dev/null &
}
function la_snippet_url_wrapper() {
    snippet_z_bind_wrapper la_snippet_url
}
zle -N la_snippet_url_wrapper
bindkey '^ku' la_snippet_url_wrapper

# vim: se fdm=marker:
